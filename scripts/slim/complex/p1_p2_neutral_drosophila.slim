// set up a simulation
initialize() {
	// initialize tree sequence recording
	initializeTreeSeq();
	defineConstant("burnin", 25000);

	// draw mutation rate 
	initializeMutationRate(0);
	
	// set up mutation classes
	initializeMutationType("m1", 0.5, "f", 0.0);  //neutral

	// make the genomic element types
	initializeGenomicElementType("g1", c(m1), c(1)); //unconstrained
	
	initializeGenomicElement(g1, 0, 9999); // all neutral

	// read recombinationr ate
        lines = readFile("rfile_"+rep+".txt");
	rates = NULL;
	ends = NULL;
	for (line in lines)
	{
		components = strsplit(line, "\t");
		ends = c(ends, asInteger(components[0]));
		rates = c(rates, asFloat(components[1]));
	}
	rates = rates * 100; // to scale
	initializeRecombinationRate(rates, ends);
}

// create a population of 125000 individuals
1 early() {
	sim.addSubpop("p1", 1250);
	sim.addSubpopSplit("p2", 1250, p1); 
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);

	community.rescheduleScriptBlock(s1, asInteger(divtime) - asInteger(tmig), asInteger(divtime) - asInteger(tmig));
	community.rescheduleScriptBlock(s2, asInteger(divtime) - asInteger(tmig)+1, asInteger(divtime) - asInteger(tmig)+1);
	community.rescheduleScriptBlock(s3, asInteger(divtime), asInteger(divtime));
}


// migration pulse
s1 12625 early() {
	p2.setMigrationRates(p1, pmig);
}

s2 12626 early() {
	p2.setMigrationRates(p1, 0);
}


s3 13750 late() {
	cat("Final random seed: " + getSeed() + "\n");
	name = "p1_p2_neutral_drosophila_"+rep+".trees";
	sim.treeSeqOutput(name);
}
