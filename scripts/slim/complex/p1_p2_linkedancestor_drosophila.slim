// set up the first simulation
// this is a pulse of migration from population 1 into population 2 
// there is a sweep in the ancestor
// neutral mutations are added after the fact via tree sequence recording


initialize() {
	// initialize tree sequence recording
	initializeTreeSeq();// 
	
	defineConstant("selcoef", runif(1, 0.1, 0.5));
	
	
	// we'll overlay our. neutral mutations, so this is zero.
	initializeMutationRate(0);
	
	// m1 mutation type: advantageous
	initializeMutationType("m3", 1.0, "f", selcoef);
	
	// g1 genomic element type: uses m3 for all mutations
	initializeGenomicElementType("g1", m3, 1.0);
	
	// uniform chromosome of length 10 kb with uniform recombination
	initializeGenomicElement(g1, 0, 9999);

	// read recombinationr ate
	lines = readFile("rfile.txt" );
	rates = NULL;
	ends = NULL;
	for (line in lines)
	{
		components = strsplit(line, "\t");
		ends = c(ends, asInteger(components[0]));
		rates = c(rates, asFloat(components[1]));
	}
	rates = rates * 100; // to scale
	initializeRecombinationRate(rates, ends);
}

// create a population of 1250 individuals and introduce a beneficial mutation
1 {
	sim.addSubpop("p1", 1250);
	defineConstant("simID", getSeed());


	sim.rescheduleScriptBlock(s1, asInteger(divtime) - asInteger(tmig)+1000 - 1, asInteger(divtime) - asInteger(tmig)+1000 - 1);
	sim.rescheduleScriptBlock(s2, asInteger(divtime) - asInteger(tmig)+1000, asInteger(divtime) - asInteger(tmig)+1000);
	sim.rescheduleScriptBlock(s3, asInteger(divtime)+1000, asInteger(divtime)+1000);
}

1 late() {
	target = sample(p1.genomes, 1);
	target.addNewDrawnMutation(m3, 5000);
	sim.treeSeqOutput("/tmp/"+rep+"slim_" + simID + ".trees");
}



1:1000 late() {
	if (sim.countOfMutationsOfType(m3) == 0) {
		if (sum(sim.substitutions.mutationType == m3) == 1) {
			cat(getSeed() + ": FIXED\n");
			sim.deregisterScriptBlock(self);
		} else {
			cat(getSeed() + ": LOST - RESTARTING\n");
			sim.readFromPopulationFile("/tmp/"+rep+"slim_" + simID + ".trees");
			setSeed(rdunif(1, 0, asInteger(2^62) - 1));
		}
	}
}
1000 late(){
	if (sum(sim.substitutions.mutationType == m3) != 1) {
		cat(getSeed() + ": NOT FIXED - RESTARTING\n");
		sim.readFromPopulationFile("/tmp/"+rep+"slim_" + simID + ".trees");
		setSeed(rdunif(1, 0, asInteger(2^62) - 1));
	}
}


// split the two populations
1001 {
	sim.addSubpopSplit("p2", 1250, p1);
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);		
}

// migration pulse
s1 12625 {
	p2.setMigrationRates(p1, pmig);
}

s2 12626 {
	p2.setMigrationRates(p1, 0);
}




s3 16250 late() {
	cat("Final random seed: " + getSeed() + "\n");
	cat("Selection_coefficient: " + selcoef);
	name = "p1_p2_linkedancestor_drosophila_"+rep+".trees";
	sim.treeSeqOutput(name);
}

