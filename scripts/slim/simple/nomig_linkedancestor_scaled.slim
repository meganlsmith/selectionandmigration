// set up the first simulation
// this is a pulse of migration from population 1 into population 2
// there is a sweep in the ancestor
// neutral mutations are added after the fact via tree sequence recording


initialize() {
	// initialize tree sequence recording
	initializeTreeSeq();

	defineConstant("selcoef", runif(1, 0.1, 0.5));

	// we'll overlay our. neutral mutations, so this is zero.
	initializeMutationRate(0);

	// m3 mutation type: advantageous
	initializeMutationType("m3", 1.0, "f", selcoef);


	// g1 genomic element type: uses m1 for all mutations
	initializeGenomicElementType("g1", m3, 1.0);

	// uniform chromosome of length 10 kb with uniform recombination
	initializeGenomicElement(g1, 0, 9999);
	initializeRecombinationRate(5e-6);
}

// create a population of 1250 individuals and introduce a beneficial mutation
1 {
	sim.addSubpop("p1", 1250);
	defineConstant("simID", getSeed());


	sim.rescheduleScriptBlock(s3, asInteger(divtime)+1000, asInteger(divtime)+1000);
}

1 late() {
	target = sample(p1.genomes, 1);
	target.addNewDrawnMutation(m3, 5000);
	sim.treeSeqOutput("/tmp/"+rep+"slim_" + simID + ".trees");
}


1:1000 late() {
	if (sim.countOfMutationsOfType(m3) == 0) {
		if (sum(sim.substitutions.mutationType == m3) == 1) {
			cat(getSeed() + ": FIXED\n");
			sim.deregisterScriptBlock(self);
		} else {
			cat(getSeed() + ": LOST - RESTARTING\n");
			sim.readFromPopulationFile("/tmp/"+rep+"slim_" + simID + ".trees");
			setSeed(rdunif(1, 0, asInteger(2^62) - 1));
		}
	}
}
1000 late(){
	if (sum(sim.substitutions.mutationType == m3) != 1) {
		cat(getSeed() + ": NOT FIXED - RESTARTING\n");
		sim.readFromPopulationFile("/tmp/"+rep+"slim_" + simID + ".trees");
		setSeed(rdunif(1, 0, asInteger(2^62) - 1));
	}
}


// split the two populations
1001 {
	sim.addSubpopSplit("p2", 1250, p1);
	sim.treeSeqRememberIndividuals(sim.subpopulations.individuals);
}




s3 16250 late() {
	cat("Final random seed: " + getSeed() + "\n");
	cat("Selection_coefficient: " + selcoef);
	name = "nomig_linkedancestor_scaled_"+rep+".trees";
	sim.treeSeqOutput(name);
}
